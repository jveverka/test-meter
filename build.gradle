plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'one.microproject.testmeter'
version = '0.0.1'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies() {
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation('org.junit.jupiter:junit-jupiter-params:5.7.1')

    testImplementation('org.slf4j:slf4j-simple:1.7.30')
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.1')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.1')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.7.1')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

ext.isSnapshot = version.endsWith("-SNAPSHOT")
if (!project.hasProperty('ossrhUsername') || !project.hasProperty('ossrhPassword')) {
    ext.ossrhUsername = "none";
    ext.ossrhPassword = "****";
    ext.isPublishVersion = false;
} else {
    ext.isPublishVersion = true;
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

tasks.withType(Sign) {
    onlyIf { isPublishVersion }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'one.microproject.testmeter'
            artifactId = 'test-meter'
            //version = ...

            from components.java
            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = 'Parallel scenario testing for JUnit 5'
                description = 'Parallel scenario testing for JUnit 5.'
                url = 'https://github.com/jveverka/test-meter/tree/master'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/jveverka/test-meter/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'jveverka'
                        name = 'Juraj Veverka'
                        email = 'gergej123@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jveverka/rpi-projects.git'
                    developerConnection = 'scm:git:git@github.com:jveverka/rpi-projects.git'
                    url = 'https://github.com/jveverka/test-meter'
                }
            }
        }
    }
    repositories {
        maven {
            if (isSnapshot) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.mavenJava
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
